plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	//spotless estandariza el formato del código
    id 'com.diffplug.spotless' version '6.21.0'
	//errorprone detecta posibles bugs en tiempo de compilación
	id 'net.ltgt.errorprone' version '3.1.0'
	//checkstyle aplica reglas de estilo
	id 'checkstyle'
}

group = 'michaelsoftbinbows'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'io.github.cdimascio:java-dotenv:5.2.2'
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.postgresql:postgresql:42.7.3'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
	implementation 'org.json:json:20240303'
	errorprone "com.google.errorprone:error_prone_core:2.31.0"
    compileOnly "com.google.errorprone:error_prone_annotations:2.31.0"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += "-parameters"
	if(options.hasProperty("errorprone")){
		options.errorprone.enabled = true
    	options.errorprone.disableWarningsInGeneratedCode = true
	}
}

spotless {
	java {
		googleJavaFormat('1.17.0')
		removeUnusedImports()
		trimTrailingWhitespace()
		target 'src/**/*.java'
		endWithNewline()
        encoding 'UTF-8'
        lineEndings 'UNIX'
	}
    format 'misc', {
        target('*.md', '*.gradle', '.gitignore')
        trimTrailingWhitespace()
        endWithNewline()
    }
}

checkstyle {
    toolVersion = '10.20.0'
    configFile = file("$projectDir/config/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
        html.outputLocation = file("$buildDir/reports/checkstyle/checkstyle.html")
    }
}
